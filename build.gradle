plugins {
    id "java"
    id "maven-publish"
    id "org.embulk.embulk-plugins" version "0.6.2"
}

// apply from: 'https://raw.githubusercontent.com/hata/gradle-plugins/master/embulk-integration-test.gradle'
apply from: 'embulk-integration-test.gradle'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

repositories {
    mavenCentral()
}

version = "0.4.0"

dependencies {
    // https://mvnrepository.com/artifact/org.embulk/embulk-api
    compileOnly "org.embulk:embulk-api:0.10.43"
    // https://dev.embulk.org/embulk-spi/
    compileOnly "org.embulk:embulk-spi:0.10.43"
    // https://dev.embulk.org/embulk-util-file/
    compile "org.embulk:embulk-util-file:0.1.5"
    // https://dev.embulk.org/embulk-util-config/
    compile "org.embulk:embulk-util-config:0.3.4"
    // https://dev.embulk.org/embulk-util-timestamp/
    compile "org.embulk:embulk-util-timestamp:0.2.2"

    testImplementation "org.embulk:embulk-api:0.10.43"
    testImplementation "org.embulk:embulk-spi:0.10.43"
    testImplementation "org.embulk:embulk-util-file:0.1.5"
    testImplementation "org.embulk:embulk-util-timestamp:0.2.2"
    testImplementation "org.jmockit:jmockit:1.15"
    testImplementation "junit:junit:4.+"
}

embulkPlugin {
    mainClass = "org.embulk.filter.SpeedometerFilterPlugin"
    category = "filter"
    type = "speedometer"
}

gem {
    authors = [ "hata" ]
    email = ["hiroki.ata@gmail.com"]
    summary = "Speedometer filter plugin for Embulk"
    description = "Write log message of processed bytes and throughput periodically."
    homepage = "https://github.com/hata/embulk-filter-speedometer"
    licenses = ["MIT"]
}

gemPush {
    host = "https://rubygems.org"
}

// To use integrationTest, run like this:
// ./gradlew -DenableIntegrationTest=true clean all

gem.dependsOn(check)

// integrationTest reference build/gemContents to run the tests.
// So, it is required to depend on gem target to generate the contents.
integrationTest.dependsOn(gem)

task all(dependsOn: ["gem", "check", "integrationTest"])

